############
# ~/.zshrc #
############

# Charge les couleurs
autoload -U colors
colors

autoload -Uz vcs_info

autoload -Uz compinit
compinit

#autoload zkbd

zmodload zsh/complist

magic-abbrev-expand() {
    local MATCH
    LBUFFER="${LBUFFER%%(#m)[_a-zA-Z0-9]#}"
    LBUFFER+="${abbreviations[$MATCH]:-$MATCH}"
    zle self-insert
}

no-magic-abbrev-expand() {
  LBUFFER+=' '
}

zle -N magic-abbrev-expand
zle -N no-magic-abbrev-expand
bindkey ' ' magic-abbrev-expand
bindkey '^x ' no-magic-abbrev-expand
#############################
# Configuration des options #
#############################

# Permet d'ajouter les répertoires dans les quels on navigue dans une pile
# de manière automatique
setopt AUTO_PUSHD
setopt PUSHDMINUS
#setopt printeightbit
setopt CORRECTALL # Correction des commandes
setopt COMPLETE_IN_WORD # Complétion à l'interieur d'un mot
setopt ALWAYS_TO_END # Positionne à la fin lors de la complétion
setopt APPENDHISTORY # Ajout les historiques
# Pour avoir le même historique dans tous les shells
setopt INC_APPEND_HISTORY SHARE_HISTORY
# Mode verbose (date, ...) ; incompatible avec les autres shells
setopt EXTENDED_HISTORY

setopt AUTOCD # Faire un cd sans cd
setopt NOTIFY # Notifie instantanément les fin de process
setopt EXTENDEDGLOB # Permet d'utiliser le find de zsh
setopt NULL_GLOB
setopt GLOB_DOTS
setopt NOHUP

unsetopt HIST_VERIFY # Supprime la vérification lors de l'usage de "!cmd"
unsetopt LIST_AMBIGUOUS # Modifie la manière dont est gérée l'autocomplétement

# Supprime les  répétitions dans le fichier  d'historique, ne conservant
# que la dernière occurrence ajoutée
setopt HIST_IGNORE_ALL_DUPS

# La recherche dans  l'historique avec l'éditeur de commandes  de zsh ne
# montre  pas  une même  ligne  plus  d'une fois,  même  si  elle a  été
# enregistrée
setopt HIST_FIND_NO_DUPS

# Permet d'utiliser des variables en prompt
setopt PROMPT_SUBST

# Gestion des raccourcis (emacs ou vi)
# les deux formes fonctionnent
set -o emacs
#bindkey -v

# Améliore les messages d'auto-complètement
zstyle :compinstall filename '${HOME}/.zshrc'
zstyle ':completion:*:descriptions' format '%U%B%d%b%u'
zstyle ':completion:*:warnings' format '%BDésolé, rien de trouvé pour: %d%b'
zstyle ':completion:*:*:kill:*:processes' list-colors "=(#b) #([0-9]#)*=36=31"

# Modifie la forme des infos de CVS
zstyle ':vcs_info:*' actionformats '%F{5}(%f%s%F{5})%F{3}-%F{5}[%F{2}%b%F{3}|%F{1}%a%F{5}]%f '
zstyle ':vcs_info:*' formats '%F{5}(%f%s:%b%F{5})%F{3}-%F{5}[%F{2}%r/%S%F{5}]%f '
zstyle ':vcs_info:(sv[nk]|bzr):*' branchformat '%b%F{1}:%F{3}%r'

#########################
## Définition du prompt #
#########################
export PROMPT='%{$fg[red]%}%n%{$reset_color%}@%M:%B%{$fg[blue]%}%~%b%{$reset_color%} ${vcs_info_msg_0_}%f%(?.. [%?])%# '
export PROMPT='%{$fg[red]%}%n%{$reset_color%}@%M:%B%{$fg[blue]%}%~%b%{$reset_color%}%([%?])%# '
export RPROMPT="[%{$fg[green]%}%*%{$reset_color%}]"
export PS2='>'

precmd (){
  vcs_info
  if [ -z "${vcs_info_msg_0_}" ]; then
    export PROMPT='%{$fg[red]%}%n%{$reset_color%}@%M:%B%{$fg[blue]%}%~%b%{$reset_color%} ${vcs_info_msg_0_}%f%(?.. [%?])%# '
  else
    export PROMPT='%{$fg[red]%}%n%{$reset_color%}@%M:%{$reset_color%}${vcs_info_msg_0_}%f%(?.. [%?])%# '
  fi
}

export PAGER=less # less est mon pager par défaut

typeset -Ag abbreviations
abbreviations=(
  'Ia'    '| awk'
  'Ig'    '| grep'
  'Ip'    "| $PAGER"
  'Ih'    '| head'
  'It'    '| tail'
  'Is'    '| sort'
  'Iw'    '| wc'
)

################################
# Initialisation des variables #
################################

# Gestion de lhistorique
HISTFILE=~/.histfile
HISTSIZE=10000
SAVEHIST=${HISTSIZE}

function addPath () {
  [[ -d "$1" ]] && PATH="${PATH}:$1"
}

function addPathPrim () {
  [[ -d "$1" ]] && PATH="$1:${PATH}"
}

function setIfDirExist () {
  [[ -d "$2" ]] && export "$1"="$2"
}

function setDirAliasIfExist() {
    [[ -d "$2" ]] && hash -d "$1"="$2"
}

# Pas de coredump
limit coredumpsize 0

###################################
# Chargement des fichiers annexes #
###################################

# Alias
[[ -f ~/.zsh_aliases ]] && source ~/.zsh_aliases

# Fonctions
[[ -f ~/.zsh_functions ]] && source ~/.zsh_functions

# Permet d'avoir la completion après la commande start
compdef _complete_start _start
alias start='nocorrect _start'

#########################
# Configuration de less #
#########################

# Permettre à less di lire plusieurs formats
eval $(lesspipe)

export LESS="-FSRX" # voir man
# Permet d'avoir les man en couleur
export LESS_TERMCAP_mb=$'\E[01;31m'    # début de blink
export LESS_TERMCAP_md=$'\E[01;31m'    # début de gras
export LESS_TERMCAP_me=$'\E[0m'        # fin
export LESS_TERMCAP_so=$'\E[01;44;33m' # début de la ligne d`état
export LESS_TERMCAP_se=$'\E[0m'        # fin
export LESS_TERMCAP_us=$'\E[01;32m'    # début de souligné
export LESS_TERMCAP_ue=$'\E[0m'        # fin

export LANG="fr_FR.UTF8"
export LANGUAGE="fr_FR.UTF8"

XZ_OPT=-e9

setIfDirExist XDG_DATA_HOME "$HOME/.data"
setIfDirExist XDG_CONFIG_HOME "$HOME/.config/"

source /usr/share/autojump/autojump.zsh

autoload zmv # permet le renommage massif facilement
autoload zargs # équivalent de xargs mieux intégré au shell

#wmname LG3D
export EDITOR=vim

## Configuration Java
export _JAVA_AWT_WM_NONREPARENTING=1
export IDEA_JDK='/usr/lib/jvm/java-11-openjdk-amd64'
addPathPrim "${JAVA_HOME}/bin"
addPathPrim "${HOME}/.local/bin"

if [[ -d "${HOME}/.zsh/completion" ]]; then
    fpath=(~/.zsh/completion $fpath)
fi

addPath "${HOME}/.aws"
addPath "${HOME}/bin"

export TAR_OPTIONS=--force-local

export GTK2_RC_FILES="$HOME/.gtkrc-2.0"
export GTK_THEME=Adwaita:dark

source /usr/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh

if [[ -d "${HOME}/Projects/SFR/sun/" ]]; then
  hash -d sun="${HOME}/Projects/SFR/sun/"
fi
hash -d scratches="${HOME}/.config/JetBrains/IntelliJIdea2020.1/scratches/"

if [[ -d "${HOME}/.nvm" ]]; then
  export NVM_DIR="$HOME/.nvm"
  [[ -s "$NVM_DIR/nvm.sh" ]] && source "$NVM_DIR/nvm.sh"  # This loads nvm
  [[ -s "$NVM_DIR/bash_completion" ]] && source "$NVM_DIR/bash_completion"  # This loads nvm bash_completion
fi


# Configure ssh user agent 
USER_SSH_AGENT="/run/user/${UID}/ssh_agent"
if [[ ! -f "${USER_SSH_AGENT}" ]] ; then
  ssh-agent | grep -v echo > "${USER_SSH_AGENT}"
  sed 's/;.*//' "${USER_SSH_AGENT}" > "${USER_SSH_AGENT}2"
fi
[[ -f "${USER_SSH_AGENT}" ]] && source "${USER_SSH_AGENT}"
